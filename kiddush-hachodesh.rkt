#lang sicp
(#%require "functions.rkt")
(#%provide (all-defined))
(define % modulo)

(define (make-interval d s c)
  (list d s c))

(define (days i)
  (car i))

(define (shaos i)
  (cadr i))

(define (chalokim i)
  (caddr i))

(define max-chalokim 1080)
(define max-shaos 24)
(define max-days 7)
(define machzor 19)
(define no-time (make-interval 0 0 0))
(define chalokim-to-minute 18)

(define (add a b)
  (let ((total-chalokim (+ (chalokim a) (chalokim b)))
        (total-shaos (+ (shaos a) (shaos b)))
        (total-days (+ (days a) (days b))))
    (let ((actual-shaos (if (>= total-chalokim max-chalokim)
                            (+ 1 total-shaos)
                            total-shaos)))
      (let ((actual-days (if (>= actual-shaos max-shaos)
                             (+ 1 total-days)
                             total-days)))
        (make-interval (% actual-days max-days)
                       (% actual-shaos max-shaos)
                       (% total-chalokim max-chalokim))))))

(define (mul-rec i n)
  (if (<= n 0)
      no-time
      (if (= n 1)
          i
          (add i (mul i (- n 1))))))

(define (mul i n)
  (if (<= n 0)
      no-time
      (let ((total-chalokim (* (chalokim i) n)))
         (let ((total-shaos (if (>= total-chalokim max-chalokim)
                                (+ (floor (/ total-chalokim max-chalokim)) (* (shaos i) n))
                                (* (shaos i) n))))
           (let ((total-days (if (>= total-shaos max-shaos)
                                 (+ (floor (/ total-shaos max-shaos)) (* (days i) n))
                                 (* (days i) n))))
             (make-interval (% total-days max-days)
                            (% total-shaos max-shaos)
                            (% total-chalokim max-chalokim)))))))

(define (leap-year? y)
  (if (> y machzor)
      (leap-year? (% y machzor))
      (or (= y 3) (= y 6) (= y 8) (= y 11) (= y 14) (= y 17) (= y 19))))

(define (calculate-remaining-years yr)
    (cond ((= yr 0) no-time)
          ((leap-year? yr) (add leap-year-remainder (calculate-remaining-years (- yr 1))))
          (else (add reg-year-remainder (calculate-remaining-years (- yr 1))))))

(define day-names (list "Shabbos" "Sunday" "Monday" "Tuesday" "Wednesday" "Thursday" "Friday"))
(define (pretty-print-interval i)
  (let ((minutes (floor (/ (chalokim i) chalokim-to-minute)))
        (chalokim (% (chalokim i) chalokim-to-minute))
        (day (if (<= (shaos i) 6)
                 (- (days i) 1)
                 (days i)))
        (am-pm (if (or (<= (shaos i) 6) (>= (shaos i) 18))
                   "PM"
                   "AM"))
        (hour (cond ((<= (shaos i) 6) (+ (shaos i) 6))
                    ((and (> (shaos i) 6) (<= (shaos i) 18))
                     (- (shaos i) 6))
                    ((> (shaos i) 18) (- (shaos i) 18)))))
    (display (list-ref day-names day))
    (display " ")
    (display hour)
    (display ":")
    (display minutes)
    (display am-pm)
    (cond ((> chalokim 0)
           (display ", ")
           (display chalokim)
           (display " chalokim")))))
        
               
                           
(define first-molad (make-interval 2 5 204))
(define month-remainder (make-interval 1 12 793))
(define machzor-remainder (make-interval 2 16 595))
(define reg-year-remainder (make-interval 4 8 876))
(define leap-year-remainder (make-interval 5 21 589))

(define (get-molad m y)
  (let ((machzors (floor (/ y machzor)))
        (remaining-years (% y machzor)))
    (add (add first-molad (mul machzor-remainder machzors))
         (add (calculate-remaining-years (- remaining-years 1)) (mul month-remainder m)))))

(define (get-rh y)
  (define (gt-interval a b) (and (= (days a) (days b)) (>= (shaos a) (shaos b)) (>= (chalokim a) (chalokim b))))
  (define (past-chatzos? molad) (>= (shaos molad) 18))
  (define (test-adu d)
    (define (is-adu? d) (or (= d 1) (= d 4) (= d 6)))
    (% (if (is-adu? d) (+ 1 d) d) max-days))
  (let ((year-molad (get-molad 0 y))
        (three-nine (make-interval 3 9 204))
        (two-fifteen (make-interval 2 15 589)))
    (cond
      ((past-chatzos? year-molad) (test-adu (+ 1 (days year-molad))))
      ((or
        (and (not leap-year?) (gt-interval year-molad three-nine))
        (and (leap-year? (- y 1)) (gt-interval year-molad two-fifteen))
        )
        (test-adu (+ (days year-molad) 1)))
      (else (test-adu (days year-molad))))))

(define (year-type y)
  (define (days-between d1 d2)
    (define (do-test d n)
      (if (= (% d max-days) d2)
          n
          (do-test (+ d 1) (+ n 1))))
    (let ((diff (do-test (+ d1 1) 0)))
      (if (= diff 0)
             5
             diff)))
  (let ((rh1 (get-rh y))
        (rh2 (get-rh (+ y 1))))
    (let ((diff (days-between rh1 rh2)))
      (cond
        ((= rh1 3) 1)
        ((leap-year? y)
             (cond ((= diff 4) 0)
                   ((= diff 5) 1)
                   ((= diff 6) 2)))
            (else (cond ((= diff 2) 0)
                        ((= diff 3) 1)
                        ((= diff 4) 2)))))))


(define (is-month-full? m y)
  (let ((year (year-type y)))
  (cond ((= m 1)
         (or (= year 2) (= year 1)))
        ((= m 2) (= year 2))
        ((or (= m 0) (= m 3)) #t)
        ((leap-year? y) (even? m))
        (else (odd? m)))))